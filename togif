#!/usr/bin/env zsh
set -e

width=-1
height=-1
fps=-1
stats_mode_diff=0

progname=${0:t}
usage() {
    cat <<EOF
USAGE: $progname [-w WIDTH] [-h HEIGHT] [-f FPS] [-d]

-d: pass 'stats_mode=diff' to 'palettegen' filter
EOF
    exit 2
}

while getopts 'w:h:f:d' option; do
    case $option in
    w) width=$OPTARG ;;
    h) height=$OPTARG ;;
    f) fps=$OPTARG ;;
    d) stats_mode_diff=1 ;;
    *) usage ;;
    esac
done
shift $(( OPTIND - 1 )) || usage
(( $# != 2 )) && usage
input=$1
output=$2

params=
if (( width >= 0 || height >= 0 )); then
    params+="scale=${width}:${height}:flags=lanczos,"
fi
if (( fps >= 0 )); then
    params+="fps=${fps},"
fi

palette=pal.png
palettegen_args=
if (( stats_mode_diff )); then
    palettegen_args="stats_mode=diff"
fi

ffmpeg -y -i $input -vf ${params}palettegen${palettegen_args:+"=$palettegen_args"} $palette
ffmpeg -y -i $input -i $palette -filter_complex \
    "${params%,}[x];[x][1:v]paletteuse=dither=none" $output
